class TeleporterEffects : EventHandler
{
    const TELEPORT_SPECIAL = 70;
    const EXIT_SPECIAL = 243;
    const EXIT_SPECIAL_SECRET = 244;
    
    const WALK_ACTIVATION = 1;
    const MONSTER_ACTIVATION = 4;
    
    Actor SpawnFountain(Vector3 pos, bool player, bool exit, bool monster, bool dest)
    {
        let fountain = TeleporterEffect(Actor.Spawn("TeleporterEffect",pos));
        fountain.SetDest(dest);
        fountain.SetPlayerStart(player);
        fountain.SetExit(exit);
        fountain.SetMonster(monster);
        
        if (teleporter_debug)
            Console.Printf("Spawning fountain at "..pos.." (exit: "..exit..", monster: "..monster..", player: "..player..", dest: "..dest..")");
        
        return fountain;
    }
    
    private bool IsTitlemap()
    {
        return gamestate == 7;
    }
    
    override void PlayerEntered (PlayerEvent e)
    {
        if (!e.IsReturn && !IsTitlemap())
        {
            let player = players[e.PlayerNumber].mo;
            SpawnFountain(player.pos, true, false, false, false);
        }
    }
    
    override void WorldThingSpawned(WorldEvent e)
    {
        bool dest = e.Thing is "TeleportDest";
        if(dest && !IsTitlemap())
            SpawnFountain(e.Thing.pos, false, false, false, true);
    }

    override void WorldLoaded(WorldEvent e)
    {   
        if (e.IsReopen || IsTitlemap())
            return;
            
        int sectors = level.sectors.size();
        
        for (int i = 0; i < sectors;i++)
        {
            Sector sect = level.sectors[i];
            vector2 center = sect.CenterSpot;
            bool walk = false;
            bool monster = false;
            bool exit = false;
            bool usable = false;
            //int repeatable = 0;
            
            Sector centerSector = level.PointInSector(center);
            
            //center point must be in the same sector, otherwise it's a weird multi-area sector
            if (centersector == null || centerSector.Index() != sect.Index())
            {
                if (teleporter_debug)
                    Console.Printf("Sector #"..sect.Index().."'s midpoint is outside of sector. Skipping...");
                continue;
            }
            
            
            for (int j = 0; j < sect.lines.size();j++)
            {
                int special = sect.lines[j].Special;
                int activation = sect.lines[j].Activation;
                //repeatable += sect.lines[j].Flags & Line.ML_REPEAT_SPECIAL ? 1 : 0;
            
                walk = walk || activation == WALK_ACTIVATION;
                monster = monster || activation == MONSTER_ACTIVATION;
                exit = exit || special == EXIT_SPECIAL || special == EXIT_SPECIAL_SECRET;
                usable = usable || special == TELEPORT_SPECIAL;
                
                //Console.Printf("Repeatable: "..repeatable);
            }
            
            //we are likely in a sector that contains teleporters, but isn't a teleporter itself
            if ((monster && exit) || (walk && monster) || (usable && exit))
                continue;
            
            if ((walk || monster) && (usable || exit))
                SpawnFountain((center.x, center.y, 0), false, exit, monster, false);
        }
    }
}
